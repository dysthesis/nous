{
  description = "A note-taking tool";

  outputs = inputs @ {
    flake-parts,
    crane,
    advisory-db,
    ...
  }:
    flake-parts.lib.mkFlake {inherit inputs;} {
      imports = [
        ./nix/pkgs
        ./nix/checks
        ./nix/shell
        inputs.treefmt-nix.flakeModule
      ];

      systems = [
        "x86_64-linux"
        "aarch64-linux"
        "aarch64-darwin"
        "x86_64-darwin"
      ];

      perSystem = {system, ...}: let
        pkgs = import inputs.nixpkgs {
          inherit system;
          overlays = [inputs.rust-overlay.overlays.default];
        };
        craneLib = crane.mkLib pkgs;
        # Clean source while keeping all workspace members.
        src = craneLib.cleanCargoSource ./.;
        commonArgs = {
          inherit src;
          strictDeps = true;
          buildInputs =
            [
              # Add additional build inputs here
            ]
            ++ pkgs.lib.optionals pkgs.stdenv.isDarwin [pkgs.libiconv];
          nativeBuildInputs = [pkgs.pkg-config];
        };
        cargoArtifacts = craneLib.buildDepsOnly commonArgs;
      in {
        _module.args = {
          inherit
            pkgs
            craneLib
            commonArgs
            cargoArtifacts
            src
            advisory-db
            ;
        };

        treefmt = {
          programs = {
            nixfmt.enable = true;
            rustfmt.enable = true;
          };
        };
      };
    };

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs";
    flake-parts.url = "github:hercules-ci/flake-parts";

    # Rust tooling
    crane.url = "github:ipetkov/crane";
    rust-overlay = {
      url = "github:oxalica/rust-overlay";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    advisory-db = {
      url = "github:rustsec/advisory-db";
      flake = false;
    };

    # Formatting
    treefmt-nix.url = "github:numtide/treefmt-nix";
  };
}
